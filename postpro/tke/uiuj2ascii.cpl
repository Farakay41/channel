IF COMMANDLINE.HI > 0 THEN
  IF COMMANDLINE(1)="-h" THEN
    WRITE "This program converst the output tke.bin of tke.cpl"
    WRITE "into several human-readable ASCII files that represent"
    WRITE "the energy box. Please start this program in the folder"
    WRITE "containing your dns.in. Syntax:"
    WRITE ""
    WRITE "uiuj2ascii [1 1 localhost -n number]"
    WRITE ""
    WRITE "If the program is started specifying -n number, file"
    WRITE "uiuj.number.bin will be converted to ascii files with"
    WRITE "the corresponding number in the filename."
    STOP
  END IF
END IF

! END OF HELP/DOCUMENTATION
!------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

gamma=0
outinterv=10
USE ../dnsdata
USE ../dnsdirect
!USE rtchecks
USE ../convenience

! Unpack input
! ------------------------------------
BOOLEAN from_instant = FALSE
INTEGER fldno
IF COMMANDLINE.HI=5 THEN
  IF COMMANDLINE(4)="--n" OR COMMANDLINE(4)="-n" THEN
    from_instant = TRUE
    fldno = atoi(COMMANDLINE(5))
  END IF
END IF

! Definitions
! ------------------------------------
#define sumint(g) [SUM yintf(g(*,i)) FOR i=1 TO 3]
ARRAY(1..6) OF STRING terms, intterms
terms(1)="profiles/uu.dat"; terms(2)="profiles/vv.dat"; terms(3)="profiles/ww.dat"; terms(4)="profiles/uv.dat"; terms(5)="profiles/uw.dat"; terms(6)="profiles/vw.dat"
intterms(1)="profiles/uuintegrals.dat"; intterms(2)="profiles/vvintegrals.dat"; intterms(3)="profiles/wwintegrals.dat";
intterms(4)="profiles/uvintegrals.dat"; intterms(5)="profiles/uwintegrals.dat"; intterms(6)="profiles/vwintegrals.dat"
IF from_instant THEN
  terms(1)=WRITE("instant_profiles/uu."fldno".dat"); terms(2)=WRITE("instant_profiles/vv."fldno".dat"); terms(3)=WRITE("instant_profiles/ww."fldno".dat")
  terms(4)=WRITE("instant_profiles/uv."fldno".dat"); terms(5)=WRITE("instant_profiles/uw."fldno".dat"); terms(6)=WRITE("instant_profiles/vw."fldno".dat")
  intterms(1)=WRITE("instant_profiles/uuintegrals."fldno".dat"); intterms(2)=WRITE("instant_profiles/vvintegrals."fldno".dat"); intterms(3)=WRITE("instant_profiles/wwintegrals."fldno".dat");
  intterms(4)=WRITE("instant_profiles/uvintegrals."fldno".dat"); intterms(5)=WRITE("instant_profiles/uwintegrals."fldno".dat"); intterms(6)=WRITE("instant_profiles/vwintegrals."fldno".dat")
END IF
MEANTERMS = STRUCTURE(REAL U,W,Uy,Wy,Uyy,Wyy,P)
MKETERMS = STRUCTURE(REAL pump,produv,prodvw,ttrsp,vdiff,dissU,dissW,PHIttrsp,PHIvdiff)
BALANCE = STRUCTURE(ARRAY(1..6) OF REAL var,prod,psdiss,ttrsp,vdiff,pstrain,ptrsp,PHIttrsp,PHIvdiff,PHIptrsp)
POINTER TO STORED STRUCTURE[
        INTEGER nmin, nmax, deltan, ntot
        ARRAY(-1..ny+1) OF MEANTERMS meandata
        ARRAY(-1..ny+1) OF MKETERMS mkedata
        ARRAY(-1..ny+1) OF BALANCE uiujdata
] uiujimage

! Open file
! ------------------------------------
STRING fname
IF from_instant THEN
  fname = WRITE("instant_profiles/uiuj."fldno".bin")
ELSE
  fname = "profiles/uiuj.bin"
END IF
uiujimage = OPEN(fname)

! Write to ASCII file
! ------------------------------------
! reynolds tensor
LOOP FOR i=1 TO 6
  FILE outfile=CREATE(terms(i))
  WITH uiujimage WRITE TO outfile "Averaged on fields from " nmin " to " nmax " with dn=" deltan "; total number of fields = " ntot
  WRITE TO outfile "y","var","prod","psdiss","ttrsp","vdiff","pstrain","ptrsp","PHIttrsp","PHIvdiff","PHIptrsp"
  DO WITH uiujimage.uiujdata(iy):
    WRITE TO outfile y(iy),var(i),prod(i),psdiss(i),ttrsp(i),vdiff(i),pstrain(i),ptrsp(i),PHIttrsp(i),PHIvdiff(i),PHIptrsp(i)
  FOR iy=0 TO ny
  CLOSE(outfile)
REPEAT
! reynolds tensor - integrals
LOOP FOR i=1 TO 6
  FILE outfile=CREATE(intterms(i))
  WITH uiujimage WRITE TO outfile "Averaged on fields from " nmin " to " nmax " with dn=" deltan "; total number of fields = " ntot
  WRITE TO outfile "var","prod","psdiss","ttrsp","vdiff","pstrain","ptrsp"
  WITH uiujimage.uiujdata: WRITE TO outfile yintf(var(*,i)),yintf(prod(*,i)),yintf(psdiss(*,i)),yintf(ttrsp(*,i)),
                                            yintf(vdiff(*,i)),yintf(pstrain(*,i)),yintf(ptrsp(*,i))
  CLOSE(outfile)
REPEAT
! tke
IF from_instant THEN
  fname=WRITE("instant_profiles/tke."fldno".dat")
ELSE
  fname="profiles/tke.dat"
END IF
FILE outfile = CREATE(fname)
WITH uiujimage WRITE TO outfile "Averaged on fields from " nmin " to " nmax " with dn=" deltan "; total number of fields = " ntot
WRITE TO outfile "y","var","prod","psdiss","ttrsp","vdiff","pstrain","ptrsp","PHIttrsp","PHIvdiff","PHIptrsp"
DO WITH uiujimage.uiujdata(iy):
  WRITE TO outfile y(iy),SUM(var(1..3))/2,SUM(prod(1..3))/2,SUM(psdiss(1..3))/2,SUM(ttrsp(1..3))/2,SUM(vdiff(1..3))/2,
                   SUM(pstrain(1..3))/2,SUM(ptrsp(1..3))/2,SUM(PHIttrsp(1..3))/2,SUM(PHIvdiff(1..3))/2,SUM(PHIptrsp(1..3))/2
FOR iy=0 TO ny
CLOSE(outfile)
! tke - integrals
IF from_instant THEN
  fname=WRITE("instant_profiles/tkeintegrals."fldno".dat")
ELSE
  fname="profiles/tkeintegrals.dat"
END IF
FILE outfile = CREATE(fname)
WITH uiujimage WRITE TO outfile "Averaged on fields from " nmin " to " nmax " with dn=" deltan "; total number of fields = " ntot
WRITE TO outfile "var","prod","psdiss","ttrsp","vdiff","pstrain","ptrsp"
WITH uiujimage.uiujdata(*): WRITE TO outfile sumint(var)/2,sumint(prod)/2,sumint(psdiss)/2,sumint(ttrsp)/2,sumint(vdiff)/2,sumint(pstrain)/2,sumint(ptrsp)/2
CLOSE(outfile)
! mke
IF from_instant THEN
  fname=WRITE("instant_profiles/mke."fldno".dat")
ELSE
  fname="profiles/mke.dat"
END IF
FILE outfile = CREATE(fname)
WITH uiujimage WRITE TO outfile "Averaged on fields from " nmin " to " nmax " with dn=" deltan "; total number of fields = " ntot
WRITE TO outfile "y","pump","produv","prodvw","ttrsp","vdiff","dissU","dissW","PHIttrsp","PHIvdiff"
DO WITH uiujimage.mkedata(iy): WRITE TO outfile y(iy),pump,produv,prodvw,ttrsp,vdiff,dissU,dissW,PHIttrsp,PHIvdiff FOR iy=0 TO ny
CLOSE(outfile)
! mke - integrals
IF from_instant THEN
  fname=WRITE("instant_profiles/mkeintegrals."fldno".dat")
ELSE
  fname="profiles/mkeintegrals.dat"
END IF
FILE outfile = CREATE(fname)
WITH uiujimage WRITE TO outfile "Averaged on fields from " nmin " to " nmax " with dn=" deltan "; total number of fields = " ntot
WRITE TO outfile "pump","produv","prodvw","ttrsp","vdiff","dissU","dissW"
WITH uiujimage.mkedata: WRITE TO outfile yintf(pump),yintf(produv),yintf(prodvw),yintf(ttrsp),yintf(vdiff),yintf(dissU),yintf(dissW)
CLOSE(outfile)
! var
IF from_instant THEN
  fname=WRITE("instant_profiles/var."fldno".dat")
ELSE
  fname="profiles/var.dat"
END IF
FILE outfile = CREATE(fname)
WITH uiujimage WRITE TO outfile "Averaged on fields from " nmin " to " nmax " with dn=" deltan "; total number of fields = " ntot
WRITE TO outfile "y","uu","vv","ww","uv","uw","vw"
DO WITH uiujimage.uiujdata(iy): WRITE TO outfile y(iy),var(1),var(2),var(3),var(4),var(5),var(6) FOR iy=0 TO ny
CLOSE(outfile)
! mean
IF from_instant THEN
  fname=WRITE("instant_profiles/mean."fldno".dat")
ELSE
  fname="profiles/mean.dat"
END IF
FILE outfile = CREATE(fname)
WITH uiujimage WRITE TO outfile "Averaged on fields from " nmin " to " nmax " with dn=" deltan "; total number of fields = " ntot
WRITE TO outfile "y","U","W","Uy","Wy","Uyy","Wyy"
DO WITH uiujimage.meandata(iy): WRITE TO outfile y(iy),U,W,Uy,Wy,Uyy,Wyy FOR iy=0 TO ny
CLOSE(outfile)
CLOSE(uiujimage)
